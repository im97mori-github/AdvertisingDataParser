plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def target = peripheral

android {
    compileSdk globalCompileSdkVersion
    buildToolsVersion = globalBuildToolsVersion

    defaultConfig {
        minSdkVersion globalPeripheralMinSdkVersion
        targetSdk globalTargetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
    }

    buildTypes {
        staging {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

    useLibrary 'android.test.mock'

    publishing {
        singleVariant("staging") {
        }
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    namespace 'org.im97mori.ble.peripheral'
    testNamespace 'org.im97mori.ble.peripheral.test'
}

dependencies {
    api apiCoreConstantsDescriptor
    api apiCoreConstantsErrorCode
    api apiAdvertisingAndroid
    api apiHandler
    api gson
    coreLibraryDesugaring desugar
    androidTestImplementation apiTestPeripheral
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = myGroupId
            artifactId = target['artifactId'].toString()
            version = uploadVersion

            afterEvaluate {
                if (staging) {
                    from components.staging
                } else {
                    from components.release
                }
            }

            pom {
                name = 'BLE Utility for Android(ble-peripheral)'
                url = 'https://github.com/im97mori-github/AndroidBLEUtil'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        email = 'github@im97mori.org'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url System.getenv('MAVEN_REPOSITORY_URL')
        }
    }
}
